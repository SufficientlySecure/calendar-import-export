apply plugin: 'com.android.application'

android {

    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 28
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29 // retain pre-Android 11 storage model at the moment
        versionCode 60
        versionName "2.7.2"
    }

    dependencies {
        implementation 'androidx.annotation:annotation:1.3.0'
        implementation 'androidx.core:core:1.6.0'
        implementation 'androidx.fragment:fragment:1.3.6'
        implementation 'org.mnode.ical4j:ical4j:3.2.1'
        implementation 'org.slf4j:slf4j-jdk14:1.7.24'
        implementation 'org.joda:joda-convert:1.5'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources {
            excludes += [
                'META-INF/INDEX.LIST',
                'META-INF/groovy/org.codehaus.groovy.runtime.ExtensionModule',
                'META-INF/groovy-release-info.properties'
            ]
        }
    }

    configurations {
        all {
            //no inspection DuplicatePlatformClasses
            exclude module: 'commons-logging'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    lint {
        abortOnError false
    }


    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

}
