apply plugin: 'com.android.application'

android {

    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 59
        versionName "2.7.1"
    }

    dependencies {
        implementation 'androidx.annotation:annotation:1.3.0'
        implementation 'androidx.core:core:1.7.0'
        implementation 'androidx.fragment:fragment:1.4.1'
        implementation 'org.mnode.ical4j:ical4j:2.2.6'
        implementation 'javax.cache:cache-api:1.1.1'
        implementation 'org.slf4j:slf4j-jdk14:1.7.24'
        implementation 'backport-util-concurrent:backport-util-concurrent:3.1'
        implementation 'commons-codec:commons-codec:1.15'
        implementation 'commons-lang:commons-lang:2.6'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    configurations {
        all {
            //no inspection DuplicatePlatformClasses
            exclude module: 'commons-logging'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    lint {
        abortOnError false
    }


    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

}
